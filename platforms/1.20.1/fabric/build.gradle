plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version="$parent.mod_version" + "-fabric.$parent.fabric_patch_version"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(':platforms:1.20.1:common').loom.accessWidenerPath
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$parent.fabric_loader_version"
    modApi "net.fabricmc.fabric-api:fabric-api:$parent.fabric_api_version"

    common(project(path: ':platforms:1.20.1:common', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':platforms:1.20.1:common', configuration: 'transformProductionFabric')) { transitive false }
    common(project(path: ':platforms:1.20.1:fabric-like', configuration: 'namedElements')) { transitive false }
    shadowCommon(project(path: ':platforms:1.20.1:fabric-like', configuration: 'transformProductionFabric')) { transitive false }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

shadowJar {
    exclude 'architectury.common.json'

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(':platforms:1.20.1:common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree it }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}